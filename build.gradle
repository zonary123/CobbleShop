plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

loom {
    splitEnvironmentSourceSets()

    mods {
        "cobbleshop" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = project.mod_name
    versionNumber = project.mod_version
    versionType = "release"
    uploadFile = tasks.named("remapJar").flatMap { it.archiveFile }.get().asFile
    gameVersions = ["1.21.1"]
    loaders = ["fabric"]
    changelog = System.getenv("CHANGELOG") ?: "Automatic upload."
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url "https://jitpack.io" }
    maven { url "https://cursemaven.com" }
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.architectury.dev/" }
    maven { url "https://repo.maven.apache.org/maven2/" }
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://repo.essentialsx.net/releases/" }
    maven { url "https://maven.neoforged.net/releases/" }
    maven { url "https://files.minecraftforge.net/maven/" }
    maven { url "https://thedarkcolour.github.io/KotlinForForge/" }
    maven { url "https://papermc.io/repository/maven-public/" }
    maven { url "https://maven.impactdev.net/repository/development/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://maven.nucleoid.xyz/"; name = "Nucleoid" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots"; name = "Sonatype Snapshots" }
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots"; name = "Sonatype 01 Snapshots" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "dev.architectury:architectury-fabric:${project.architectury_version}"
    modImplementation("com.cobblemon:fabric:${property("cobblemon_version")}")
    api("net.objecthunter:exp4j:0.4.8")
    modImplementation(files(fileTree(dir: 'libs', include: '*.jar')))
    implementation("com.github.ben-manes.caffeine:caffeine:3.2.2")
    modImplementation("ca.landonjw.gooeylibs:fabric-api-repack:${property("gooeylibs_version")}")

    // Economy APIs
    modImplementation("net.impactdev.impactor.api:economy:${property("impactor_version")}")
    modImplementation("net.impactdev.impactor:common:${property("impactor_version")}")
    api("org.spigotmc:spigot-api:${property("spigot_version")}")
    api("com.github.MilkBowl:VaultAPI:${property("vault_api_version")}")

    // Placeholder API
    modImplementation("eu.pb4:placeholder-api:${property("placeholder_api_version_fabricandforge")}")

    // Database
    modImplementation("com.zaxxer:HikariCP:5.0.1")
    implementation("org.mongodb:mongodb-driver-sync:${property("mongodb_version")}")

    // Lombok
    annotationProcessor("org.projectlombok:lombok:${property("lombok_version")}")
    implementation("org.projectlombok:lombok:${property("lombok_version")}")

    // Votifier
    implementation("com.github.NuVotifier.NuVotifier:nuvotifier-api:2.7.2")
    implementation("com.github.DrexHD:NuVotifier-Fabric:1.0.0-1.20.5")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.fabric_loader_version
    inputs.property "mod_version", project.mod_version
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_name", project.mod_name
    inputs.property "author", project.author
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.fabric_loader_version,
                "mod_version": project.mod_version,
                "mod_name": project.mod_name,
                "mod_id": project.mod_id,
                "author": project.author
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java
        }
    }
}
